function syntaxerr Error('Usage: pNMRproc foldspec.in <file> <number of segments> <shift points> <zero order phase> <first order phase> [<selection range> [<linewidth> [<zerofillfac>]]]')

function foldspec_ if(size(#3),if(head(#3),head(#1,#2),0)+foldspec_(tail(#1,#2),#2,tail(#3)),0) LSL
function foldspec foldspec_(#1,size(#1)/size(#2),#2) LL
function rollspec [tail(#1,#2),head(#1,#2)] LS
function gsim_to_SIMPSON_phase [#1+#2/2,-#2]
function inrange_ if(#1 < #2, 0, #1 < #3+1)
function inrange if(size(#1),if(inrange_(head(#1),#2,#3),inrange(tail(#1),#2,#3),0),1) LSS

par {
	variable checkargs $(4?syntaxerr())
	sw $sw/$1
	echo Folded spectral width: $sw Hz
	echo Gsim phases: zero order: $3  first order: $4
	variable zero,first gsim_to_SIMPSON_phase($3,$4)
	echo SIMPSON/pNMRsim phases: zero order: $zero  first order: $first
	variable lw $(6?20)
	variable zerofillfac $(7?2)
	variable newnp ceil($np*$zerofillfac,$1)
	echo LW: $lw Hz  Zerofill factor: $zerofillfac
	echo Original points: $np   Points after zerofill and fold: $newnp
	variable range $(5?[$5]:[1:$1])
	variable checkfold if(inrange($range,1,$1),1,Error('Selection range outside 1...number of segments'))
	variable selrange replace(repeat(0,$1),$range,1)
	echo Add range: $range  $selrange
}

proc {
	addlb $lw/$1 1
	zerofill $newnp
	ft
	phase $first $zero
	apply foldspec 1 $selrange
	apply rollspec 1 $2
	save $(name)o.spe -simplot
}
